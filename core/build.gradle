apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.sonarqube'
apply from: "$projectDir/config/jacoco-settings.gradle"

android {
    compileSdkVersion 32
    testOptions.unitTests.includeAndroidResources = true
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 3
        versionName version
        buildConfigField 'String', 'VERSION_NAME', "\"${version}\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    lintOptions {
        abortOnError true
        warningsAsErrors true
        lintConfig file("config/lint.xml")
    }
}


dependencies {

    implementation "androidx.appcompat:appcompat:1.2.0"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.10'
    testImplementation "org.mockito:mockito-core:3.2.0"
    testImplementation "com.squareup.okhttp3:mockwebserver:3.11.0"
    testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: "1.1.1"
    testImplementation 'org.hamcrest:hamcrest:2.1'
    testImplementation 'org.hamcrest:hamcrest-library:2.1'
}


task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
    options.addStringOption 'Xdoclint:none', '-quiet'
    options.addStringOption 'Xmaxwarns', '1'
    options.addStringOption 'Xmaxerrs', '1'
    options.links "https://docs.oracle.com/javase/8/docs/api"
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

def aarFile = file("$buildDir/outputs/aar/core-$version"+".aar")
def aarArtifact = artifacts.add('archives', aarFile) {
    type 'aar'
}

def isReleaseVersion = !version.endsWith('SNAPSHOT')

publishing {

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }

    publications {
        hyperwalletCore(MavenPublication) {
            groupId = 'com.hyperwallet.android'
            artifactId = 'core-sdk'
            version = version

            artifact(sourcesJar)
            artifact(javadocsJar)
            artifact(aarArtifact)

            pom {
                name = 'Hyperwallet Android Core SDK'
                description = 'Hyperwallet Core SDK for Android to integrate with Hyperwallet Platform'
                url = 'https://github.com/hyperwallet/hyperwallet-android-sdk'
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'devs'
                        name = 'Hyperwallet Developers'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/hyperwallet/hyperwallet-android-sdk.git'
                    developerConnection = 'scm:git:git://github.com/hyperwallet/hyperwallet-android-sdk.git'
                    url = 'https://github.com/hyperwallet/hyperwallet-android-sdk'
                }
            }
        }
    }
}

tasks.withType(Sign) {
    onlyIf {
        isReleaseVersion && sonatypeUsername?.trim() && sonatypePassword?.trim()
    }
}

signing {
    sign publishing.publications.hyperwalletCore
}

sonarqube {
    properties {
        def libraries = project.android.sdkDirectory.getPath() + "/platforms/android-28/android.jar"
        property "sonar.sources", "src/main/java"
        property "sonar.binaries", "build/intermediates/javac/prodRelease/compileProdReleaseJavaWithJavac/classes/com/hyperwallet/android"
        property "sonar.libraries", libraries
        !version.endsWith('SNAPSHOT')
    }
}